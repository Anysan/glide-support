task wrapper(type: Wrapper) {
	gradleVersion = '2.11'
}
project.afterEvaluate {
	tasks.withType(JavaCompile) {
		options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
	}
}

buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:1.5.0'
	}
}
apply plugin: 'com.android.application'

repositories {
	jcenter()
	// https://github.com/bumptech/glide/wiki/Snapshots
	maven { name 'glide'; url 'http://oss.sonatype.org/content/repositories/snapshots' }
}

android {
	compileSdkVersion 23
	buildToolsVersion "23.0.2"
	defaultConfig {
		minSdkVersion 10
		//noinspection OldTargetApi I only have a Samsung Galaxy S4 4.4.2 to test on
		targetSdkVersion 19
		versionCode 1
		versionName "0.1"
	}
	sourceSets.main.java.srcDir 'src/main/thirdparty'
	productFlavors {
		glide3 {
			applicationId "com.bumptech.glide.supportapp.v3"
			sourceSets.glide3.java.srcDir 'src/glide3/thirdparty'
			sourceSets.glide3.java.srcDir 'src/glide3/hacks'
			sourceSets.glide3.res.srcDir 'src/glide3/res'
		}
		glide4 {
			applicationId "com.bumptech.glide.supportapp.v4"
			sourceSets.glide4.java.srcDir 'src/glide4/thirdparty'
			sourceSets.glide4.java.srcDir 'src/glide4/hacks'
			sourceSets.glide4.res.srcDir 'src/glide4/res'
		}
	}

	buildTypes {
		release {
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_7
		targetCompatibility JavaVersion.VERSION_1_7
	}

	packagingOptions {
		exclude 'META-INF/DEPENDENCIES.txt'
		exclude 'META-INF/NOTICE'
		exclude 'META-INF/NOTICE.txt'
		exclude 'META-INF/LICENSE'
		exclude 'META-INF/LICENSE.txt'
	}

	lintOptions {
		abortOnError false
	}
}

dependencies {
	// Immediate SNAPSHOT resolution (in case the built version is too new), default is a day
	//configurations.glide4Compile.resolutionStrategy.cacheChangingModulesFor 0, 'seconds' 

	compile fileTree(dir: 'libs', include: [ '*.jar' ])
	def supportVersion = '23.2.0'

	// Basic Android dependencies
	compile "com.android.support:support-annotations:${supportVersion}"
	compile "com.android.support:support-v4:${supportVersion}"
	compile "com.android.support:appcompat-v7:${supportVersion}"

	// Extra dependencies for individual issues
	compile "com.android.support:cardview-v7:${supportVersion}"
	compile "com.android.support:recyclerview-v7:${supportVersion}"
	compile "com.android.support:palette-v7:${supportVersion}"
	compile 'com.firebase:firebase-client-android:2.5.2'

	// Glide v4 (unreleased)
	glide4Compile "com.github.bumptech.glide:glide:4.0.0-SNAPSHOT"
	// OkHttp
	glide4Compile 'com.github.bumptech.glide:okhttp3-integration:2.0.0-SNAPSHOT'
	glide4Compile 'com.squareup.okhttp3:okhttp:3.1.2'
	// Volley
	glide4Compile 'com.github.bumptech.glide:volley-integration:2.0.0-SNAPSHOT'
	glide4Compile 'com.mcxiaoke.volley:library:1.0.16'

	// Glide v3 (stable)
	glide3Compile 'com.github.bumptech.glide:glide:3.7.0'
	// OkHttp
	glide3Compile('com.github.bumptech.glide:okhttp-integration:1.4.0')
	glide3Compile 'com.squareup.okhttp:okhttp:2.7.5'
	// OkHttp3
	glide3Compile('com.github.bumptech.glide:okhttp3-integration:1.4.0') {
		exclude group: 'glide-parent'
	}
	glide3Compile 'com.squareup.okhttp3:okhttp:3.1.2'
	// Volley
	glide3Compile 'com.github.bumptech.glide:volley-integration:1.4.0'
	glide3Compile 'com.mcxiaoke.volley:library:1.0.7'

	// Competition
	compile 'com.squareup.picasso:picasso:2.5.2'
	compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
	//compile 'com.facebook.fresco:fresco:0.9.0' // not used yet, it's too many dependencies
}
